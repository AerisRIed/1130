upf_version 2.0

##############################################################################
# This is a simple UPF for a single domain block
# Set the net name here. It is assumed bias net are the same as the rail nets.
# The voltage value is accessory if the UPF will never be merged in a context with
# crossings since it's not used in timing conditions.
# 
# Supply set: SS_<pwr_net> 
# Power domain: PD_<power_net>
##############################################################################
set pwr_net avdd
set gnd_net gnd
set pwr_voltage 0.75

##############################################################################
## Design portion of the UPF
##############################################################################
set supply_nets [concat ${pwr_net} ${gnd_net}] 
foreach sn ${supply_nets} {
   create_supply_net  ${sn}
   create_supply_port ${sn}
   connect_supply_net ${sn} -ports ${sn} 
}

##############################################################################
## Create supply_sets
##############################################################################
set supply_set_name SS_${pwr_net}
create_supply_set ${supply_set_name} \
    -function [list power  ${pwr_net}] \
    -function [list ground ${gnd_net}] \
    -function [list nwell  ${pwr_net}] \
    -function [list pwell  ${gnd_net}]

##############################################################################
## Create power domains
##############################################################################
set power_domain_name PD_${pwr_net}
create_power_domain ${power_domain_name} \
    -supply [list primary ${supply_set_name}] \
    -include_scope \
    -elements {.}

##############################################################################
## Explicitly assign driver and receiver supply sets of top level boundary ports
##############################################################################
set_port_attributes -elements {.} -applies_to inputs  -driver_supply   ${supply_set_name}
set_port_attributes -elements {.} -applies_to outputs -receiver_supply ${supply_set_name}

##############################################################################
##  Define Power States and Modes
##############################################################################
add_power_state ${supply_set_name}   -state ON [subst -nocommands {-supply_expr {power == `{FULL_ON, ${pwr_voltage}} && ground == `{FULL_ON, 0.0}} -simstate NORMAL}]
add_power_state ${power_domain_name} -state PM_all_on [subst -nocommands {-logic_expr {${supply_set_name} == ON}}]

